plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id "org.sonarqube" version "4.3.0.3225"
	id 'jacoco'
}

group = 'com.ejada'
version = '0.0.1'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

ext {
	minCodeCoverage = 0.7
}

jacoco {
	toolVersion = "0.8.10"
}

tasks.jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}
}

tasks.jacocoTestCoverageVerification {
	dependsOn jacocoTestReport
	violationRules {
		rule {
			limit {
				minimum = 0.7
			}
		}
	}
}

tasks.sonarqube {
	dependsOn jacocoTestReport
}

sonarqube {
	properties {
		property "sonar.projectKey", "product-service"
		property "sonar.projectName", "product-service"
		property "sonar.sources", "./src/main"
		property "sonar.tests", "./src/test"
		property "sonar.exclusions", "**/model/**,**/exception/**,**/*Application.java,**/migration/**"
		property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/**/*.xml"
		property "sonar.host.url", "http://localhost:9000"
		property "sonar.login", "admin" // Default login is admin/admin
		property "sonar.password","1234"
	}
}
